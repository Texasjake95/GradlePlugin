
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = "com.texasjake95"
archivesBaseName = "GradlePlugin"
version = '1.1-SNAPSHOT'

jar { manifest {
	} }

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	compile gradleApi()
	compile localGroovy()
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.java
}

task javadocJar(type: Jar, dependsOn: "javadoc") {
	classifier "javadoc"
	from "${buildDir}/docs/javadoc"
}

artifacts { 
	archives sourcesJar 
	archives javadocJar
}

build.dependsOn sourcesJar, javadocJar

uploadArchives {
	dependsOn 'build'
	repositories {
		
		if(project.hasProperty("filesmaven"))
		{
			mavenDeployer {
				logger.info("Pushing to Maven Remote")
				repository(url: project.filesmaven)
				pom {
					groupId = project.group
					version = project.version
					artifactId = project.archivesBaseName
					project {
						name project.archivesBaseName
						packaging 'jar'
						description 'Plugin for Texasjake95\'s gradle scripts'
						url 'https://github.com/Texasjake95/GradlePlugin'
						
						scm {
							url 'https://github.com/Texasjake95/GradlePlugin'
							connection 'scm:git:git://github.com/Texasjake95/GradlePlugin.git'
							developerConnection 'scm:git:git@github.com:Texasjake95/GradlePlugin.git'
						}
						
						issueManagement {
							system 'github'
							url 'https://github.com/Texasjake95/GradlePlugin/issues'
						}
						
						developers {
							developer {
								id 'Texasjake95'
								name 'Texasjake95'
								roles { role 'developer' }
							}
						}
					}
				}
			}
		}
		else{
			logger.info("Pushing to Maven Local")
			mavenDeployer {
				repository(url: project.repositories.mavenLocal().url)
			}
		}
	}
}